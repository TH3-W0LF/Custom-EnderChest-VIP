import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    java
    id("com.gradleup.shadow") version "8.3.5"
}

group = "com.alkacode"
version = "1.0.16"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/")
    maven("https://jitpack.io")
    maven("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    maven("https://repo.extendedclip.com/content/repositories/placeholderapi/")
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    // moeda ALKARION vem do AlkaEconomy - substitui o Vault generico do plugin legado
    // (publicado via `./gradlew publishToMavenLocal` no projeto AlkaEconomy). AlkaCore
    // e necessario em compileOnly tambem porque AlkaEconomyPlugin agora estende
    // com.alkacode.core.plugin.AlkaPlugin - o javac precisa da hierarquia completa
    // pra resolver o cast/instanceof AlkaEconomyPlugin, mesmo sem usar API do Core direto.
    // banco/HikariCP vem do AlkaCore (DatabaseProvider) - AlkaEnderChest nao abre
    // conexao JDBC propria nem embarca driver.
    compileOnly("com.alkacode:AlkaCore:1.0.8")
    compileOnly("com.alkacode:AlkaEconomy:1.0.8")
    compileOnly("me.clip:placeholderapi:2.11.6")
}

tasks.withType<JavaCompile> {
    options.encoding = "UTF-8"
    options.release.set(21)
}

tasks.named<ShadowJar>("shadowJar") {
    archiveClassifier.set("")
}

tasks.build {
    dependsOn(tasks.shadowJar)
}

tasks.processResources {
    filteringCharset = "UTF-8"
    // sem isso, o Gradle nao percebe que so `version` mudou e reusa o plugin.yml
    // antigo do cache (processResources fica UP-TO-DATE incorretamente).
    inputs.property("version", project.version)
    filesMatching("plugin.yml") {

        expand("version" to project.version)

    }
}
